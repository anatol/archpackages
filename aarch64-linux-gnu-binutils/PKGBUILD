# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

_target=aarch64-linux-gnu
pkgname=$_target-binutils
pkgver=2.24
pkgrel=1
pkgdesc='A set of programs to assemble and manipulate binary and object files for the ARM64 target'
arch=(i686 x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(glibc)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.bz2
        fix_compile_error.patch)
sha256sums=('e5e8c5be9664e7f7f96e0d09919110ab5ad597794f5b1809871177a0f0f14137'
            'ba182841d9c5416870ffd9a08eda9c93ca71a6bd9ad1c9bd292f0d3606e5e3b4')

prepare() {
  cd binutils-$pkgver
  patch -p1 < "$srcdir"/fix_compile_error.patch
}

build() {
  cd binutils-$pkgver

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  ./configure --target=$_target \
              --program-prefix=$_target- \
              --prefix=/usr \
              --enable-shared \
              --disable-multilib \
              --disable-nls \
              --with-lib-path=/usr/lib/binutils/$_target \
              --with-local-prefix=/usr/lib/$_target \
              --with-sysroot=/usr/$_target \
              --disable-werror

  make configure-host
  make
}

check() {
  cd binutils-$pkgver

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check
}

package() {
  cd binutils-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,nlmconv,windres,windmc}*

  # Remove info documents that conflict with host version
  rm -rf "$pkgdir"/usr/share/info
}
