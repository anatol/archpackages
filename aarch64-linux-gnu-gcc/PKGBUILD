# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

_target=aarch64-linux-gnu
pkgname=$_target-gcc
pkgver=4.9.1
pkgrel=2
_snapshot=4.9-20140903
pkgdesc='The GNU Compiler Collection - cross compiler for ARM64 target'
arch=(i686 x86_64)
url='http://gcc.gnu.org/'
license=(GPL LGPL FDL)
depends=($_target-binutils $_target-eglibc libmpc elfutils)
options=(!emptydirs !strip staticlibs)
conflicts=($_target-gcc-stage1 $_target-gcc-stage2)
replaces=($_target-gcc-stage1 $_target-gcc-stage2)
provides=($_target-gcc-stage1=$pkgver $_target-gcc-stage2=$pkgver)
source=(#ftp://gcc.gnu.org/pub/gcc/releases/gcc-$pkgver/gcc-$pkgver.tar.bz2
        ftp://gcc.gnu.org/pub/gcc/snapshots/$_snapshot/gcc-$_snapshot.tar.bz2
       )
sha256sums=('9f9d13ab2daeb5d511ebba7cef2477272789e54028ddb80ce193a11c4c37974e')

if [ -n "$_snapshot" ]; then
  _basedir=gcc-$_snapshot
else
  _basedir=gcc-$pkgver
fi

prepare() {
  cd "$srcdir/$_basedir"
  echo $pkgver > gcc/BASE-VER

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" "$srcdir"/$_basedir/{libiberty,gcc}/configure
}

build() {
  cd $_basedir
  mkdir build
  cd build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  ../configure \
    --prefix=/usr \
    --program-prefix=$_target- \
    --target=$_target \
    --enable-shared \
    --disable-nls \
    --enable-threads=posix \
    --enable-languages=c,c++ \
    --enable-multilib \
    --disable-libssp \
    --disable-libquadmath \
    --with-sysroot=/usr/$_target \
    --with-build-sysroot=/usr/$_target \
    --with-as=/usr/bin/$_target-as \
    --with-ld=/usr/bin/$_target-ld \
    --enable-interwork \
    --disable-libgomp \
    --enable-__cxa_atexit \
    --enable-addons

  make
}

package() {
  cd $_basedir/build

  make DESTDIR="$pkgdir" install

  # Strip host objects but do not touch target one
  find "$pkgdir" -executable -type f -exec /usr/bin/strip --strip-debug '{}' \;

  # Remove files that conflict with host gcc package
  rm -r "$pkgdir"/usr/share/man/man7
  rm -r "$pkgdir"/usr/share/info
  rm -r "$pkgdir"/usr/share/gcc-$pkgver
}
