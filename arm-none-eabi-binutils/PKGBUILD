# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

# Based on the summon-arm-toolchain script by Piotr Esden-Tempski
# https://github.com/esden/summon-arm-toolchain

pkgname=arm-none-eabi-binutils
pkgver=2.24
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files for the ARM EABI (bare-metal) target."
arch=(i686 x86_64)
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('arm-none-eabi-toolchain')
depends=('zlib')
replaces=('cross-arm-none-eabi-binutils')
options=(!libtool !emptydirs)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2)
sha256sums=('e5e8c5be9664e7f7f96e0d09919110ab5ad597794f5b1809871177a0f0f14137')

build() {
  cd binutils-${pkgver}

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  ./configure --target=arm-none-eabi \
              --prefix=/usr \
              --enable-multilib \
              --enable-interwork \
              --with-gnu-as \
              --with-gnu-ld \
              --disable-nls

  make configure-host
  make
}

package() {
  cd binutils-${pkgver}

  make DESTDIR=${pkgdir} install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm ${pkgdir}/usr/share/man/man1/arm-none-eabi-{dlltool,nlmconv,windres,windmc}*

  # Remove info documents
  rm -rf ${pkgdir}/usr/share/info
}
