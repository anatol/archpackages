# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

# Based on the summon-arm-toolchain script by Piotr Esden-Tempski
# https://github.com/esden/summon-arm-toolchain

pkgname=arm-none-eabi-gcc
pkgver=4.8.2
pkgrel=7
#_snapshot=4.8-20130411
_newlibver=2.1.0
pkgdesc="The GNU Compiler Collection - cross compiler for ARM EABI (bare-metal) target."
arch=(i686 x86_64)
url="http://gcc.gnu.org/"
license=(GPL LGPL FDL)
makedepends=('arm-none-eabi-binutils' 'gmp' 'mpfr' 'libmpc')
options=(!libtool !emptydirs !strip staticlibs zipman docs)
source=(ftp://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        #ftp://gcc.gnu.org/pub/gcc/snapshots/${_snapshot}/gcc-${_snapshot}.tar.bz2
        ftp://sourceware.org/pub/newlib/newlib-${_newlibver}.tar.gz
        gcc-${pkgver}-multilib.patch
        gcc-${pkgver}-no-exceptions.patch
        libgloss-configure-fix.patch)
sha256sums=('09dc2276c73424bbbfda1dbddc62bbbf900c9f185acf7f3e1d773ce2d7e3cdc8'
            '3e4d5ab9f0508942b6231b8ade4f8e5048cf92c96ed574c2bd6bd3320a599a48'
            '4edd875c89391cbf5c2b57587a51da0dfda54ca0c711a85e9c301f03083e4189'
            '649eb3b443e975d40ca61128ba715d1e3a29716e1e45a52c661ac0dba3c2099d'
            'eac96c034d3d7d16080d9aef4749e4eda493563a3348e6a06f08e8a330b027ba')

if [ -n "${_snapshot}" ]; then
  _basedir=gcc-${_snapshot}
else
  _basedir=gcc-${pkgver}
fi

prepare() {
  # Apply configure fix from newlib CVS
  cd "${srcdir}/newlib-${_newlibver}/libgloss/arm"
  patch -Np0 -i "${srcdir}/libgloss-configure-fix.patch"
  autoreconf

  cd "${srcdir}/${_basedir}"
  patch -Np1 -i "${srcdir}/gcc-${pkgver}-multilib.patch"
  patch -Np1 -i "${srcdir}/gcc-${pkgver}-no-exceptions.patch"

  # Move newlib and libgloss to gcc source directory
  mv "${srcdir}/newlib-${_newlibver}/newlib"   "${srcdir}/${_basedir}"
  mv "${srcdir}/newlib-${_newlibver}/libgloss" "${srcdir}/${_basedir}"
}

build() {
  export CFLAGS="-O2"
  export CXXFLAGS="-O2"
  unset CPPFLAGS

  cd "${srcdir}/${_basedir}"

  mkdir build
  cd build

  ../configure --target=arm-none-eabi \
               --prefix=/usr \
               --libexecdir=/usr/lib \
               --with-pkgversion="Arch User Repository" \
               --with-bugurl="https://aur.archlinux.org/packages/arm-none-eabi-gcc" \
               --enable-multilib \
               --enable-interwork \
               --enable-languages=c,c++ \
               --with-newlib \
               --with-gnu-as \
               --with-gnu-ld \
               --disable-nls \
               --disable-shared \
               --disable-threads \
               --with-headers=newlib/libc/include \
               --disable-libssp \
               --disable-libstdcxx-pch \
               --disable-libmudflap \
               --disable-libgomp \
               --with-system-zlib \
               --disable-newlib-supplied-syscalls

  make
}

package() {
  cd ${_basedir}/build

  make DESTDIR=${pkgdir} install

  # Remove files that conflict with host gcc package
  rm "${pkgdir}/usr/lib/libiberty.a"
  rm -r "${pkgdir}/usr/share/man/man7"
  rm -r "${pkgdir}/usr/share/info"
  rm -r "${pkgdir}/usr/share/gcc-${pkgver}"
}
