# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

# Based on the summon-arm-toolchain script by Piotr Esden-Tempski
# https://github.com/esden/summon-arm-toolchain

pkgname=arm-none-eabi-gdb
pkgver=7.7
pkgrel=2
pkgdesc="The GNU Debugger for the ARM EABI (bare-metal) target."
arch=(i686 x86_64)
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
groups=('arm-none-eabi-toolchain')
depends=('ncurses' 'expat' 'python2')
makedepends=('texinfo')
optdepends=('openocd: for debugging JTAG targets'
            'stlink: for debugging over STLINK')
source=(ftp://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.bz2)
sha256sums=('0404d1c6ee6b3ddd811722b5197944758d37b4591c216030effbac204f5a6c23')

build() {
  cd gdb-${pkgver}

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  ./configure --target=arm-none-eabi \
              --prefix=/usr \
              --enable-multilib \
              --enable-interwork \
              --with-system-readline \
              --disable-nls \
              --with-python=/usr/bin/python2 \
              --with-system-gdbinit=/etc/gdb/gdbinit
  make
}

package() {
  cd gdb-${pkgver}

  make DESTDIR=${pkgdir} install

  # Following files conflict with 'gdb' package
  rm -r ${pkgdir}/usr/share/info
  # TOTHINK: we remove python module used for debugging. It means arm-*-gdb alone will not be able to debug and 'gdb' package
  # should be installed. File a bug upstream and ask separate python module folders for cross tools.
  rm -r ${pkgdir}/usr/share/gdb
  rm -r ${pkgdir}/usr/include/gdb
  rm ${pkgdir}/usr/share/man/man1/gdb*
  rm -r ${pkgdir}/usr/share/man/man5
}
