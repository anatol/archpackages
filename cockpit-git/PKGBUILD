# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cockpit-git
pkgver=r119.f783a51
pkgrel=1
pkgdesc='a user interface for Linux servers'
arch=(i686 x86_64)
url='http://www.cockpit-project.org/'
license=(GPL21)
depends=(libssh systemd)
makedepends=(git intltool json-glib python2-pyscss gtk-doc gobject-introspection)
source=(git://github.com/cockpit-project/cockpit)
sha1sums=('SKIP')

pkgver() {
  cd cockpit
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd cockpit
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

check() {
  cd cockpit
  make check
}

package() {
  cd cockpit
  make DESTDIR="$pkgdir" install
}
