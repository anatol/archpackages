# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
# Contributor: Simon Lipp <aur@simon.lipp.name>
# Contributor: Chris Bolton <http://scr.im/chris-kun>
# Contributor: Sergei Lebedev <superbobry at gmail dot com>
# Contributor: flying sheep <flying-sheep@web.de>

pkgname=coffee-script
pkgver=1.6.3
pkgrel=1
pkgdesc='CoffeeScript is a little language that compiles into JavaScript.'
arch=(any)
url=http://coffeescript.org
license=(MIT)
depends=(nodejs)
optdepends=('nodejs-profile: to load coffee-script in node')
source=(https://github.com/jashkenas/coffee-script/archive/$pkgver.tar.gz)
sha256sums=('59fa721aa1968603822058d8f5d8077595fb108a47ccdb95d553edcdbc67e321')

check() {
  cd coffee-script-$pkgver
  bin/cake test:browser
}

package() {
  cd coffee-script-$pkgver

  # cake writes bullshit to ~/.noode-libraries, so set HOME to local dir
  HOME="$PWD" bin/cake --prefix "$pkgdir/usr/" install

  install -d $pkgdir/usr/lib/node_modules
  mv $pkgdir/usr/lib/coffee-script $pkgdir/usr/lib/node_modules/
  ln -sf /usr/lib/node_modules/coffee-script/bin/{cake,coffee} $pkgdir/usr/bin/

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/${pkgname}/README"
}
