# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cyassl
pkgver=2.9.4
pkgrel=1
pkgdesc='A small, fast, portable implementation of TLS/SSL for embedded devices to the cloud'
arch=(i686 x86_64)
license=(GPL)
url='https://www.yassl.com/'
source=(https://github.com/cyassl/cyassl/archive/v$pkgver.zip)
sha1sums=('b37fd3661756742ab3c761a80781589c36bf80f8')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --disable-fastmath \
              --disable-fasthugemath --disable-bump \
              --enable-opensslextra --enable-fortress \
              --enable-keygen --enable-certgen \
              --disable-debug --disable-ecc \
              --disable-ntru

  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
