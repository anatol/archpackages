# Maintainer: Ã˜yvind 'Mr.Elendig' Heggstad <mrelendig@har-ikkje.net>
# Contributor: Fabrizio Antonangeli <fabrizio.antonangeli@gmail.com>

pkgname=dfu-util
pkgver=0.7
pkgrel=2
pkgdesc='Tool intended to download and upload firmware using DFU protocol to devices connected over USB'
url='http://dfu-util.gnumonks.org'
license=(GPL2)
arch=(i686 x86_64)
depends=(udev libusbx)
install=dfu-util.install
source=(stm32dfu.udev.rules
        http://dfu-util.gnumonks.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('1af7470c261e8909152270ef5452fd05b1014c53'
          'a6f621a17a164bd86d2752e2fcd3903f94a8925b')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/stm32dfu.udev.rules "${pkgdir}"/etc/udev/rules.d/48-stm32dfu.rules
}
