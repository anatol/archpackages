# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=fbthrift-git
pkgver=r726.454ceb2
pkgrel=1
pkgdesc='Facebooks branch of apache thrift'
arch=(i686 x86_64)
url='https://github.com/facebook/fbthrift'
license=(APACHE)
depends=(folly-git snappy)
makedepends=(git python2)
source=(git+https://github.com/facebook/fbthrift.git)
md5sums=('SKIP')

pkgver() {
  cd fbthrift
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd fbthrift/thrift
  find -name '*.py' -exec sed -i 's|^#!/usr/bin/env python\b|#!/usr/bin/env python2|' {} \;
  sed -i 's|\bpython\b|python2|g' lib/cpp2/Makefile.am lib/cpp2/test/Makefile.am
}

build() {
  cd fbthrift/thrift
  autoreconf --install
  PYTHON_VERSION=2 ./configure --prefix=/usr
  make
}

check() {
  cd fbthrift/thrift
  make check
}

package() {
  cd fbthrift/thrift
  make DESTDIR="$pkgdir" install
}

