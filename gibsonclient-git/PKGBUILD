# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=gibsonclient-git
pkgver=r29.2bd52b0
pkgrel=1
pkgdesc="Client for Gibson - a high efficiency, tree based memory cache server."
arch=(i686 x86_64)
url="http://gibson-db.in/"
license=(BSD)
depends=()
makedepends=(cmake)
source=('gibson::git://github.com/evilsocket/libgibsonclient'
        'linenoise::git://github.com/antirez/linenoise')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd $srcdir/gibson
  # repo does not have any git tags yet
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd $srcdir/gibson

  # libgison uses git submodule, handle this
  git submodule init
  git config submodule.linenoise.url $srcdir/linenoise
  git submodule update

  cmake .
  make
}

package() {
  cd $srcdir/gibson
  make install DESTDIR=$pkgdir
} 
