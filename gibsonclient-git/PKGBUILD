# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=gibsonclient-git
pkgver=0.r29.2bd52b0
pkgrel=1
pkgdesc="Client for Gibson - a high efficiency, tree based memory cache server."
arch=(i686 x86_64)
url="http://gibson-db.in/"
license=(BSD)
depends=()
makedepends=(cmake)
source=('gibson::git://github.com/evilsocket/libgibsonclient'
        'linenoise::git://github.com/antirez/linenoise')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd gibson
  # repo does not have any git tags yet
  echo 0.r$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd gibson

  # libgison uses git submodule, handle this
  git submodule init
  git config submodule.linenoise.url $srcdir/linenoise
  git submodule update

  cmake .
  make
}

package() {
  cd gibson
  make install DESTDIR=$pkgdir
} 
