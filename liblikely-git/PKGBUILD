# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=liblikely-git
pkgver=r516.18e039d
pkgrel=1
pkgdesc='An image recognition runtime for heterogenous architectures'
arch=(i686 x86_64)
url='http://liblikely.org/'
license=(Apache2)
makedepends=(cmake)
source=('likely::git://github.com/biometrics/likely.git'
        'data::git://github.com/biometrics/likely-data.git'
        'llvm::git://github.com/llvm-mirror/llvm.git'
        'lua::git://github.com/biometrics/lua.git'
        'opencv::git://github.com/Itseez/opencv.git'
        'qcustomplot::git://git.gitorious.org/qcustomplot/qcustomplot.git')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver() {
  cd likely
  # liblikely repo does not have any git tags yet
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd likely

  git submodule init
  git config submodule.data.url "$srcdir/data"
  git config submodule.llvm.url "$srcdir/llvm"
  git config submodule.lua.url "$srcdir/lua"
  git config submodule.opencv.url "$srcdir/opencv"
  git config submodule.qcustomplot.url "$srcdir/qcustomplot" 
  git submodule update

  mkdir build
}

build() {
  cd likely/build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

# llvm has problems with python3
#check() {
#  cd likely/build
#  make check
#}

package() {
  cd likely/build
  make DESTDIR="$pkgdir" install
}
