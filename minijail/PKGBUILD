# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=minijail
pkgdesc='Tool to run a process in jailed environment'
pkgver=R37.5978
_tag=release-${pkgver/\./-}.B
pkgrel=1
arch=(i686 x86_64)
url='https://chromium.googlesource.com/chromiumos/platform/minijail'
license=('custom:chromiumos')
depends=(libcap)
source=(https://chromium.googlesource.com/chromiumos/platform/minijail/+archive/refs/heads/$_tag.tar.gz)
sha1sums=('3e3b2039afe92ecfe9b58b5e9b4f439cd2b2c6fc')

prepare() {
  sed 's/-Werror//' -i Makefile
}

build() {
  CC=gcc make
}

check() {
  CC=gcc make tests
  for t in `ls *_unittest`; do ./$t; done
}

package() {
  install -m755 -D minijail0 "$pkgdir"/usr/bin/minijail0
  install -m755 -D libminijail.so "$pkgdir"/usr/lib/libminijail.so
  install -m755 -D libminijailpreload.so "$pkgdir"/usr/lib/libminijailpreload.so
  install -m644 -D libminijail.h "$pkgdir"/usr/include/libminijail.h
  install -m644 -D minijail0.1 "$pkgdir"/usr/share/man/man1/minijail0.1
  install -m644 -D minijail0.5 "$pkgdir"/usr/share/man/man5/minijail0.5
  install -m644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
