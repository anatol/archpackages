pkgname=mldonkey
pkgver=3.1.3
pkgrel=2
pkgdesc="A multi-network P2P client"
arch=('i686' 'x86_64')
url="http://mldonkey.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'file' 'gd')
makedepends=('lablgtk2' 'librsvg' 'ocaml')
optdepends=('librsvg: GUI support'
            'libx11: GUI support')
backup=('etc/conf.d/mldonkey')
options=('!makeflags')
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkeyd
        mldonkey.conf
        mldonkey.service
        mldonkey.tmpfiles)
md5sums=('671f60467a918a9b7c2affef63ff5c25'
         'aa0731149fed0813740c2da4e610fd0a'
         '65aafb5e5563961c916c6d9510bd69cd'
         '9d37df25afdefc8ee27b01e2b99e1ab3'
         'cb8336207bbbe280bedf15d548aab90f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-gui=newgui2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -d  "$pkgdir/var/lib/mldonkey"


  install -Dm644 "$srcdir/$pkgname-$pkgver/icons/rsvg/type_source_normal.svg" \
    "$pkgdir/usr/share/icons/mldonkey.svg"

  install -Dm755 "$srcdir/mldonkeyd" "$pkgdir/etc/rc.d/mldonkey"
  install -Dm644 "$srcdir/mldonkey.conf" "$pkgdir/etc/conf.d/mldonkey"
  install -Dm644 "$srcdir/mldonkey.service" "$pkgdir/usr/lib/systemd/system/mldonkey.service"
  install -Dm644 "$srcdir/mldonkey.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/mldonkey.conf"
  install -Dm644 "distrib/mldonkey.desktop" "$pkgdir/usr/share/applications/mldonkey.desktop"
}
