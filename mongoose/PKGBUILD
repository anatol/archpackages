# Contributor: Leslie P. Polzer <polzer@gnu.org>
# Contributor: bender02 at archlinux dot us

pkgname=mongoose
pkgver=3.7
pkgrel=1
pkgdesc="Small and quick-to-use web server; https/php/cgi support"
arch=('i686' 'x86_64')
license=('MIT')
url="http://code.google.com/p/mongoose/"
optdepends=('php-cgi: for php support')
backup=('etc/mongoose/mongoose.conf')
source=(http://mongoose.googlecode.com/files/$pkgname-${pkgver}.tgz
        mongoose.conf
        rc.d.mongoose
        mongoose.service)
md5sums=('7ab92453a0f36d107806c73e96383d4a'
         'f7370a5f065f16e953676ac5888d8d20'
         '2aea8278222400289576a626fd1ef33d'
         'f9243699cd7508afa2c5a4adb4b2d1d8')


build() {
  cd "$srcdir/$pkgname"

  make linux
}

package() {
  cd "$srcdir"

  # sample rc.d script and config file courtesy of
  # Alexandr Leykin: al at instrument dot pl dot ua
  install -D -m755 rc.d.mongoose "$pkgdir/etc/rc.d/mongoose"
  install -D -m644 mongoose.conf "$pkgdir/etc/mongoose/mongoose.conf"
  install -D -m644 mongoose.service "$pkgdir/usr/lib/systemd/system/mongoose.service"

  cd "$srcdir/$pkgname"

  # install the binary
  install -D -m755 mongoose "$pkgdir/usr/bin/mongoose"

  # install the docs
  install -d "$pkgdir/usr/share/mongoose"
  install -m644 UserManual.md README.md "$pkgdir/usr/share/mongoose"

  # uncomment if you want to
  # install the sources + examples to /usr/share/mongoose
  #tar -x -f $srcdir/$pkgname-$pkgver.tgz -C $pkgdir/usr/share/
}
