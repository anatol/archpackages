# Maintainer: Anatol Pomozov anatol dot pomozov at g mail
# Contributor: Leslie P. Polzer <polzer@gnu.org>
# Contributor: bender02 at archlinux dot us
# Contributor: Felix Saparelli <me@passcod.name>

pkgname=mongoose
pkgver=4.1
pkgrel=1
pkgdesc="Small and quick-to-use web server; https/php/cgi support"
arch=('i686' 'x86_64')
license=('MIT')
url="https://github.com/cesanta/mongoose"
depends=('lua' 'openssl')
optdepends=('php-cgi: for php support')
backup=('etc/mongoose/mongoose.conf')
source=("$url/archive/${pkgver}.tar.gz"
        mongoose.conf
        mongoose.service)
sha1sums=('e9c25fec4e1b9b929101201beacb9c5ba51ad78c'
          'dc53f149c21243e65dbc6e6463f9b373026e79d8'
          '493e5792257b6ea9651f1042683385d4850cc28d')

prepare() {
  export COPT="-DNO_SSL_DL -DUSE_LUA -lm -llua -lcrypto -lssl $CFLAGS $LDFLAGS"
}

build() {
  cd "$pkgname-$pkgver/build"
  make linux
}

check() {
  cd "$pkgname-$pkgver/build"
  make tests
}

package() {
  install -D -m644 mongoose.conf "$pkgdir/etc/mongoose/mongoose.conf"
  install -D -m644 mongoose.service "$pkgdir/usr/lib/systemd/system/mongoose.service"

  cd "$pkgname-$pkgver"
  # install the docs
  install -d "$pkgdir/usr/share/mongoose"
  install -m644 UserManual.md README.md "$pkgdir/usr/share/mongoose"

  cd build
  # install the binary
  install -D -m755 mongoose "$pkgdir/usr/bin/mongoose"

  # uncomment if you want to
  # install the sources + examples to /usr/share/mongoose
  #tar -x -f $srcdir/$pkgname-$pkgver.tgz -C $pkgdir/usr/share/
}
