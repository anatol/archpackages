# Maintainer: Anatol Pomozov anatol dot pomozov at g mail
# Contributor: Leslie P. Polzer <polzer@gnu.org>
# Contributor: bender02 at archlinux dot us
# Contributor: Felix Saparelli <me@passcod.name>

pkgname=mongoose
pkgver=5.1
pkgrel=2
pkgdesc='Small and quick-to-use web server; https/php/cgi support'
arch=(i686 x86_64)
license=(MIT)
url='https://github.com/cesanta/mongoose'
optdepends=('php-cgi: for php support')
backup=(etc/mongoose/mongoose.conf)
_cyassl_version=2.4.6
source=(https://github.com/cesanta/mongoose/archive/$pkgver.tar.gz
        https://github.com/cyassl/cyassl/archive/v$_cyassl_version.zip
        mongoose.conf
        mongoose.service)
sha1sums=('0a2d0b83a7f5650a6e6c50fd7ba18a06ebe79335'
          '3d0d1e15541c73c813f7f54a0b606f2fcbb2e6c8'
          'dc53f149c21243e65dbc6e6463f9b373026e79d8'
          '493e5792257b6ea9651f1042683385d4850cc28d')

build() {
  # The package provides several binaries: pure mongoose, with lua, sqlite, ssl enabled.
  # Should we ship the simplest or the fat binary?
  cd $pkgname-$pkgver/build
  CFLAGS_EXTRA="-D_GNU_SOURCE $CFLAGS $LDFLAGS" make mongoose-lua-sqlite-ssl
  mv mongoose-lua-sqlite-ssl mongoose
}

check() {
  cd $pkgname-$pkgver/build
  make unix_unit_test tests
}

package() {
  install -D -m644 mongoose.conf "$pkgdir/etc/mongoose/mongoose.conf"
  install -D -m644 mongoose.service "$pkgdir/usr/lib/systemd/system/mongoose.service"

  cd $pkgname-$pkgver
  # install the docs
  install -d "$pkgdir/usr/share/mongoose"
  install -m644 README.md "$pkgdir/usr/share/mongoose/"

  # install the binary
  install -D -m755 build/mongoose "$pkgdir/usr/bin/mongoose"

  # uncomment if you want to
  # install the sources + examples to /usr/share/mongoose
  #tar -x -f $srcdir/$pkgname-$pkgver.tgz -C $pkgdir/usr/share/
}
