# Maintainer: Anatol Pomozov anatol dot pomozov at g mail
# Contributor: Leslie P. Polzer <polzer@gnu.org>
# Contributor: bender02 at archlinux dot us
# Contributor: Felix Saparelli <me@passcod.name>

pkgname=mongoose
pkgver=5.0
pkgrel=2
pkgdesc='Small and quick-to-use web server; https/php/cgi support'
arch=(i686 x86_64)
license=(MIT)
url='https://github.com/cesanta/mongoose'
depends=(lua openssl)
optdepends=('php-cgi: for php support')
backup=(etc/mongoose/mongoose.conf)
source=(https://github.com/cesanta/mongoose/archive/$pkgver.tar.gz
        mongoose.conf
        mongoose.service
        fix_tests.diff)
sha1sums=('d05e46fc096a76c1b5c849ee4c5f644672012c88'
          'dc53f149c21243e65dbc6e6463f9b373026e79d8'
          '493e5792257b6ea9651f1042683385d4850cc28d'
          '01e409a2e4382407839d0470b758d6fb33a16548')

prepare() {
  cd $pkgname-$pkgver
  patch -p 1 < ../fix_tests.diff
}

build() {
  # The package provides several binaries: pure mongoose, with lua, sqlite, ssl enabled.
  # Should we ship the simplest or the fat binary?
  cd $pkgname-$pkgver/build
  CFLAGS_EXTRA="$CFLAGS $LDFLAGS" make mongoose
}

check() {
  cd $pkgname-$pkgver/build
  make unix_unit_test tests
}

package() {
  install -D -m644 mongoose.conf "$pkgdir/etc/mongoose/mongoose.conf"
  install -D -m644 mongoose.service "$pkgdir/usr/lib/systemd/system/mongoose.service"

  cd $pkgname-$pkgver
  # install the docs
  install -d "$pkgdir/usr/share/mongoose"
  install -m644 README.md "$pkgdir/usr/share/mongoose/"

  # install the binary
  install -D -m755 build/mongoose "$pkgdir/usr/bin/mongoose"

  # uncomment if you want to
  # install the sources + examples to /usr/share/mongoose
  #tar -x -f $srcdir/$pkgname-$pkgver.tgz -C $pkgdir/usr/share/
}
