# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mruby-git
pkgver=0.r3880.af03812
pkgrel=1
pkgdesc="Lightweight Ruby (preliminary release for internal team review)"
arch=(i686 x86_64)
url="https://github.com/mruby/mruby"
license=(MIT)
depends=()
makedepends=(bison ruby)
source=('mruby::git://github.com/mruby/mruby.git')
md5sums=('SKIP')

pkgver() {
  cd mruby
  # mruby repo does not have any git tags yet
  echo 0.r$(git rev-list --count master).$(git rev-parse --short master) 
}

build() {
  cd mruby
  make
}

check() {
  cd mruby
  make test
}

package() {
  cd mruby

  install -d $pkgdir/usr/lib
  cp -r bin $pkgdir/usr/
  cp build/host/lib/libmruby.a $pkgdir/usr/lib
  cp -r include $pkgdir/usr
  rm $pkgdir/usr/*/.gitkeep
} 

