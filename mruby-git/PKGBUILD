# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mruby-git
pkgver=r4002.8cd42f6
pkgrel=1
pkgdesc='Lightweight Ruby (preliminary release for internal team review)'
arch=(i686 x86_64)
url='https://github.com/mruby/mruby'
license=(MIT)
makedepends=(git bison ruby)
options=(staticlibs)
source=(git://github.com/mruby/mruby)
sha1sums=(SKIP)

pkgver() {
  cd mruby
  # mruby repo does not have any git tags yet
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd mruby
  make
}

check() {
  cd mruby
  make test
}

package() {
  cd mruby

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib"
  cp build/host/bin/* "$pkgdir/usr/bin"
  cp build/host/lib/*.a "$pkgdir/usr/lib"
  cp -r include "$pkgdir/usr"
}

