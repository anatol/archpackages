# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mruby-git
pkgver=1.0.0.r1503.gb1dd57e
pkgrel=1
pkgdesc='Lightweight Ruby (preliminary release for internal team review)'
arch=(i686 x86_64)
url='https://github.com/mruby/mruby'
license=(MIT)
makedepends=(git bison ruby)
options=(staticlibs)
source=(git://github.com/mruby/mruby
        remove_termcap_static_lib.patch)
sha1sums=('SKIP'
          'e86e5b3001a2e7094981757dbfb052310817b758')

pkgver() {
  cd mruby
  git describe --long --tags | sed 's/-/.r/; s/-/./'
}

prepare() {
  cd mruby
  patch -p1 < "$srcdir"/remove_termcap_static_lib.patch
}

build() {
  cd mruby
  make
}

check() {
  cd mruby
  make test
}

package() {
  cd mruby

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib"
  cp build/host/bin/* "$pkgdir/usr/bin"
  cp build/host/lib/*.a "$pkgdir/usr/lib"
  cp -r include "$pkgdir/usr"
}

