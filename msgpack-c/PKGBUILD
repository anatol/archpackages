# Contributor: Leen Jewel <leenjewel@gmail.com>
# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=0.5.9
_tag=cpp-$pkgver
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=(i686 x86_64)
url='http://msgpack.org/'
license=(Apache)
depends=(gcc-libs)
source=(https://github.com/msgpack/msgpack-c/archive/$_tag.zip)
sha1sums=('04829b0555f3e4fa7970a15518e39e6ef87363e0')

build() {
  cd msgpack-c-$_tag 
  ./bootstrap
  ./configure --prefix=/usr
  make
}

check() {
  cd msgpack-c-$_tag
  make check  
}

package() {
  cd msgpack-c-$_tag
  make DESTDIR="$pkgdir" install
}
