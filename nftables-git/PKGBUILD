# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contibutor: Steven Allen <steven@stebalien.com>

pkgname=nftables-git
pkgver=0.099.r2.g11ba325
pkgrel=1
pkgdesc='Netfilter nf_tables infrastructure library'
arch=(i686 x86_64)
url='http://netfilter.org/projects/nftables/'
license=(GPL2)
depends=(libmnl gmp libnftnl-git ncurses)
makedepends=(git docbook2x perl-xml-libxml)
source=(git://git.netfilter.org/nftables)
sha1sums=('SKIP')

pkgver() {
  cd nftables
  git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
  cd nftables
  sed 's/docbook2x-man/docbook2man/g' -i Makefile.rules.in -i configure.ac
}

build() {
  cd nftables
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd nftables
  make DESTDIR="$pkgdir" install
}
