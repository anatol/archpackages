# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=osquery
pkgver=1.0.3
pkgrel=1
pkgdesc='Tool makes low-level operating system analytics and monitoring both performant and intuitive'
arch=(i686 x86_64)
url='https://github.com/facebook/osquery/'
license=(BSD)
depends=(rocksdb google-glog thrift procps-ng gflags boost-libs)
makedepends=(cmake doxygen boost git python2-jinja libunwind)
source=(https://github.com/facebook/osquery/archive/$pkgver.zip
        git://github.com/osquery/third-party
        gflags_compile.patch
        use_shared_libs.patch)
sha1sums=('a245aaa356eb4fc6fdbdd4c487c08fe227fd191f'
          'SKIP'
          '2c024cdc01fb7c63b9f1af240fe4a3fa91f2f786'
          '9b081827be8ff2981fb806d98d137fa494234787')
# https://github.com/facebook/osquery/issues/334

prepare() {
  cd osquery-$pkgver
  patch -p1 < ../gflags_compile.patch
  patch -p1 < ../use_shared_libs.patch

  rm -r third-party
  ln -sf ../third-party

  sed -e 's|python|python2|g' -i osquery/CMakeLists.txt
}

build() {
  cd osquery-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=ON
  make
}

check() {
  cd osquery-$pkgver
  make test
}

package () {
  cd osquery-$pkgver
  make DESTDIR="$pkgdir" install
}
