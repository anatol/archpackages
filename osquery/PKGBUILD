# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=osquery
pkgver=1.1.0
pkgrel=1
pkgdesc='Tool makes low-level operating system analytics and monitoring both performant and intuitive'
arch=(i686 x86_64)
url='https://github.com/facebook/osquery/'
license=(BSD)
depends=(rocksdb google-glog thrift procps-ng gflags boost-libs rpm-org)
makedepends=(cmake doxygen boost git python2-jinja libunwind python2)
source=(https://github.com/facebook/osquery/archive/$pkgver.zip
        git://github.com/osquery/third-party
        sqlite_shared.patch
        compile_fixes.patch)
sha1sums=('9319b7b53ba106990e9864bb13c5be0ff34a49d5'
          'SKIP'
          'e006d3e50f10434a19a2f2003112e83923e8555a'
          '631d34092b7339c1bdbcb73065327bbf5f713f2d')

prepare() {
  cd osquery-$pkgver
  rm -r third-party
  ln -sf ../third-party

  patch -p1 < ../compile_fixes.patch
  patch -p1 < ../sqlite_shared.patch

  sed -e 's|python|python2|g' -i osquery/CMakeLists.txt
}

build() {
  cd osquery-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=ON -DBUILD_SHARED=ON
  make
}

check() {
  cd osquery-$pkgver
  # some tests fail
  # make test
}

package () {
  cd osquery-$pkgver
  make DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
