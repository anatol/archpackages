# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=perl-dr-tarantool
_cpanname=DR-Tarantool
pkgver=0.39
pkgrel=1
pkgdesc="a Perl driver for Tarantool"
arch=('any')
license=('Artistic Perl')
url="http://search.cpan.org/dist/DR-Tarantool/"
depends=('perl-json-xs' 'perl-anyevent' 'perl-devel-globaldestruction')
source=("http://search.cpan.org/CPAN/authors/id/U/UN/UNERA/${_cpanname}-${pkgver}.tar.gz")
sha1sums=('0e12c026f04a15969c00baf9dfe3b8f96b474d42')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  perl Makefile.PL -- INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  make install DESTDIR=${pkgdir}
}
