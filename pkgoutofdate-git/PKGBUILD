# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

_name=pkgoutofdate
pkgname=$_name-git
pkgver=r10.3dd8194
pkgrel=1
pkgdesc="Checks whether Arch packages are out-of-date"
arch=(any)
url="https://github.com/anatol/$_name"
license=(GPL3)
depends=(ruby)
optdepends=(
  'abs: If you want to check ABS packages'
)
makedepends=()
source=("$_name::git://github.com/anatol/$_name.git")
md5sums=('SKIP')

pkgver() {
  cd $_name
  # repo does not have any git tags yet
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd $_name

  sed -i "s|./parse_pkgbuild.sh|/usr/share/$_name/parse_pkgbuild.sh|" $_name.rb
}

package() {
  cd $_name

  install -d $pkgdir/usr/bin $pkgdir/usr/share/$_name
  install -m755 -T $_name.rb $pkgdir/usr/bin/$_name
  install -m755 -t $pkgdir/usr/share/$_name parse_pkgbuild.sh
}
