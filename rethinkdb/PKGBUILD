# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>

pkgname=rethinkdb
pkgver=1.10.1
pkgrel=1
pkgdesc='An open-source distributed database built with love.'
arch=('i686' 'x86_64')
url='http://www.rethinkdb.com/'
license=(AGPL)
depends=('protobuf' 'v8' 'zlib' 'termcap' 'gperftools')
makedepends=('boost' 'python')
install=rethinkdb.install
source=(
  http://download.rethinkdb.com/dist/rethinkdb-$pkgver.tgz
  rethinkdb-tmpfile.conf
  rethinkdb@.service
  fix1499.diff
)
sha256sums=('cb75088fea23f3ac63d198af790d80944bfb19b0938453d074f3776052b269e0'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            'e56bffa2b9ebc3a00ef566ab2be0719a633c89d961a2461dfa2d9ffdb258c1a2'
            'cc262cb4e3f94e10fb1894924c1049201f06d6ecc09e3654b6c6471009f0f0be')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < ../fix1499.diff
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --disable-drivers --dynamic all --enable-precompiled-web --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "$pkgname-$pkgver"
  make build/release/rethinkdb-unittest
  # skip some failing tests
  # https://github.com/rethinkdb/rethinkdb/issues/1059
  # https://github.com/rethinkdb/rethinkdb/issues/549
  ./build/release/rethinkdb-unittest --gtest_filter=-TimerTest.TestApproximateWaitTimes:RDBProtocol.OvershardedSindexCreateDrop:RDBProtocol.OvershardedMissingAttr:RDBBtree.SindexInterruptionViaDrop
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/rethinkdb-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/rethinkdb.conf"
  install -Dm644 "$srcdir/rethinkdb@.service" "$pkgdir/usr/lib/systemd/system/rethinkdb@.service"

  # create 'default' database instance
  cp "$pkgdir/etc/rethinkdb/default.conf.sample" "$pkgdir/etc/rethinkdb/instances.d/default.conf"
  sed -e 's|# bind=127.0.0.1|bind=all|' \
      -e 's|# directory=/var/lib/rethinkdb|directory=/var/lib/rethinkdb|' \
      -e 's|# pid-file=/var/run/rethinkdb/rethinkdb.pid|pid-file=/var/run/rethinkdb/default.pid|' \
      -i "$pkgdir/etc/rethinkdb/instances.d/default.conf"

  # Arch uses systemd, no need for init.d scripts
  rm -rf "$pkgdir/etc/init.d"
}
