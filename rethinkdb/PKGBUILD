# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>

pkgname=rethinkdb
pkgver=1.11.0
pkgrel=1
pkgdesc='An open-source distributed database built with love.'
arch=('i686' 'x86_64')
url='http://www.rethinkdb.com/'
license=(AGPL)
depends=('protobuf' 'v8' 'zlib' 'termcap' 'gperftools')
makedepends=('boost' 'python')
backup=(etc/rethinkdb/instances.d/default.conf)
install=rethinkdb.install
source=(
  http://download.rethinkdb.com/dist/rethinkdb-$pkgver.tgz
  rethinkdb-tmpfile.conf
  rethinkdb@.service
  v8_api_update.diff
)
sha256sums=('375a3a1ce68b388ea631dc413e472131e102c73ba8b808c98ef89492db1df8f0'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            'e56bffa2b9ebc3a00ef566ab2be0719a633c89d961a2461dfa2d9ffdb258c1a2'
            '2d88108a6c27944056df5b8697ed50578f443b16f43c82bdaa1818cd99bf0787')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < ../v8_api_update.diff
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --disable-drivers --dynamic all --enable-precompiled-web --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "$pkgname-$pkgver"
  make build/release/rethinkdb-unittest
  # skip some failing tests
  # https://github.com/rethinkdb/rethinkdb/issues/1059
  # https://github.com/rethinkdb/rethinkdb/issues/549
  ./build/release/rethinkdb-unittest --gtest_filter=-TimerTest.TestApproximateWaitTimes:RDBProtocol.OvershardedSindexCreateDrop:RDBProtocol.OvershardedMissingAttr:RDBProtocol.OvershardedOverSizedKeys:RDBBtree.SindexInterruptionViaDrop:RDBBtree.SindexPostConstruct
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/rethinkdb-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/rethinkdb.conf"
  install -Dm644 "$srcdir/rethinkdb@.service" "$pkgdir/usr/lib/systemd/system/rethinkdb@.service"

  # create 'default' database instance
  cp "$pkgdir/etc/rethinkdb/default.conf.sample" "$pkgdir/etc/rethinkdb/instances.d/default.conf"
  sed -e 's|# directory=/var/lib/rethinkdb|directory=/var/lib/rethinkdb|' \
      -e 's|# pid-file=/var/run/rethinkdb/rethinkdb.pid|pid-file=/var/run/rethinkdb/default.pid|' \
      -i "$pkgdir/etc/rethinkdb/instances.d/default.conf"

  # Arch uses systemd, no need for init.d scripts
  rm -rf "$pkgdir/etc/init.d"
}
