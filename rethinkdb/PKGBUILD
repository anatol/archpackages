# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>

pkgname=rethinkdb
pkgver=1.10.0
pkgrel=1
pkgdesc='An open-source distributed database built with love.'
arch=('i686' 'x86_64')
url='http://www.rethinkdb.com/'
license=(AGPL)
depends=('protobuf' 'v8' 'zlib' 'termcap')
makedepends=('boost' 'python' 'libunwind' 'gperftools')
install=rethinkdb.install
source=(
  http://download.rethinkdb.com/dist/rethinkdb-$pkgver.tgz
  rethinkdb-tmpfile.conf
  rethinkdb@.service
  fix1499.diff
  v8_api_update.diff
)
sha256sums=('ed3c6fbee6e4e3c00a76d09022f2ea5b25cdf1f8c2773eaa069cac2d70bc6157'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            'e56bffa2b9ebc3a00ef566ab2be0719a633c89d961a2461dfa2d9ffdb258c1a2'
            'cc262cb4e3f94e10fb1894924c1049201f06d6ecc09e3654b6c6471009f0f0be'
            'e3ec08427355e056ea034fd1e3ca33da3bc8910977f7fb8a1b0549d33db94634')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../fix1499.diff
  patch -p1 < ../v8_api_update.diff
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --disable-drivers --enable-precompiled-web --prefix=/usr --sysconfdir=/etc
  make
}

check() {
# TODO add fast integration tests. It seems requires memcached, but it is a new dependency :(
  cd "$srcdir/$pkgname-$pkgver"
# make tests require 'python2' and 'ruby-protocol-buffers' for drivers
# make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/rethinkdb-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/rethinkdb.conf"
  install -Dm644 "$srcdir/rethinkdb@.service" "$pkgdir/usr/lib/systemd/system/rethinkdb@.service"

  # create 'default' database instance
  cp "$pkgdir/etc/rethinkdb/default.conf.sample" "$pkgdir/etc/rethinkdb/instances.d/default.conf"
  sed -e 's|# bind=127.0.0.1|bind=all|' \
      -e 's|# directory=/var/lib/rethinkdb|directory=/var/lib/rethinkdb|' \
      -e 's|# pid-file=/var/run/rethinkdb/rethinkdb.pid|pid-file=/var/run/rethinkdb/default.pid|' \
      -i "$pkgdir/etc/rethinkdb/instances.d/default.conf"

  # Arch uses systemd, no need for init.d scripts
  rm -rf "$pkgdir/etc/init.d"
}
