# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Conributor: Nathan Phillip Brink <binki@gentoo.org>

_gemname=tzinfo
pkgname=ruby-$_gemname
pkgver=1.1.0
pkgrel=1
pkgdesc='A Ruby library for time zone transformations'
arch=(any)
url='http://tzinfo.github.io'
license=(MIT)
options=(!emptydirs)
depends=(ruby ruby-thread_safe)
makedepends=(rubygems)
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha1sums=('7fc79cdbbcb2bc6a71935e53c741846ca98264f6')

build() {
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --no-user-install --ignore-dependencies -i "${_gemname}${_gemdir}" ${_gemname}-${pkgver}.gem
  rm -rf "${_gemname}${_gemdir}/cache"
}

check() {
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  cd "${_gemname}${_gemdir}/gems/${_gemname}-$pkgver"
  rake test
}

package() {
  cp -r ${_gemname}/* "$pkgdir"
}
