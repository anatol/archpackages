# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Nicolas Qui√©not <niQo @ aur>
# Contributor: Falk Stern <falk@fourecks.de>

pkgname=shairport-git
pkgver=r418.26e9cad
pkgrel=1
pkgdesc='Emulates an AirPort Express for the purpose of streaming music from iTunes and compatible iPods and iPhones'
url='http://github.com/abrasive/shairport'
arch=(i686 x86_64)
license=(GPL)
backup=(etc/conf.d/shairport)
depends=(libao openssl libpulse avahi)
makedepends=(git)
source=(git://github.com/abrasive/shairport.git)
sha1sums=('SKIP')

pkgver() {
  cd shairport
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd shairport
  make
}

package() {
  cd shairport
  make PREFIX="$pkgdir/usr" install
  install -D -m644 scripts/shairport.service "$pkgdir"/usr/lib/systemd/system/shairport.service
  install -d "$pkgdir"/etc/conf.d/
  echo "# Shairport Daemon options" > "$pkgdir"/etc/conf.d/shairport
}
