# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=stlink
pkgrel=1
pkgver=1.0.0
pkgdesc='Firtmware programmer for STM32 STLINK v1/v2 protocol'
arch=(i686 x86_64)
url='https://github.com/texane/stlink'
license=(BSD)
depends=(libusb udev)
install=stlink.install
source=(https://github.com/texane/stlink/archive/$pkgver.zip)
sha256sums=('09e676948adda9fd354d0a07584ad5884bc7f8e36ddb6b2f39b2211056b7263a')

build() {
  cd stlink-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd stlink-$pkgver

  # install binaries
  make DESTDIR="$pkgdir" install

  # install additional files
  install -Dm644 "stlink_v1.modprobe.conf"   "${pkgdir}/etc/modprobe.d/stlink_v1.modprobe.conf"
  install -Dm644 "49-stlinkv1.rules"         "${pkgdir}/etc/udev/rules.d/49-stlinkv1.rules"
  install -Dm644 "49-stlinkv2.rules"         "${pkgdir}/etc/udev/rules.d/49-stlinkv2.rules"
  install -Dm644 "LICENSE"                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "README"                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
