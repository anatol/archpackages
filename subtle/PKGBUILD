# $Id: PKGBUILD 104602 2014-01-23 13:15:21Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: unexist <unexist@dorfelite.net>
# Contributor: Abakus <java5@arcor.de>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=subtle
# check the mercurial hash at http://hg.subforge.org/subtle
_hg_revision=3241
_hg_hash=71c757f2a3c4
pkgver=0.11.r$_hg_revision
pkgrel=1
pkgdesc='Grid-based manual tiling window manager'
arch=('x86_64' 'i686')
url='http://subtle.subforge.org/'
license=('GPL')
depends=('ruby' 'libxft' 'libxpm' 'libxrandr' 'libxft' 'libxpm' 'libxinerama' 'libxtst')
makedepends=('pkg-config' 'libxinerama' 'libxrandr' 'libxft' 'libxpm')
backup=('etc/xdg/subtle/subtle.rb')
install=subtle.install
source=(http://hg.subforge.org/subtle/archive/$_hg_hash.tar.bz2
        respect_arch_flags.diff
        fix_ruby21.diff
        do_not_relink_binaries_on_install.diff)
sha256sums=('dc0ec1e9989d48c3efa37fd3b448d34adf10fbac29067e89a57c94eda0281956'
            '1e0c262c7577f97c177a4b3f26dda35c4c78b127286e7467b09b2d12b7af4b3e'
            'd30dd718e5f242b0c127ac30784eddf9f4b6f8edb64d93d1505a1430e35da980'
            '448dd3bcc8fd3807ab0f66c1d82edccbeb830502dfe758770e7f7a76ffce35a0')

prepare() {
  cd $pkgname-$_hg_hash

  # Use vendor_ruby instead of site_ruby
  sed -e \
    's/RbConfig::CONFIG\["sitelibdir"\]/RbConfig::CONFIG\["vendorlibdir"\]/' \
    -i Rakefile

  patch -p1 < ../respect_arch_flags.diff
  patch -p1 < ../fix_ruby21.diff
  patch -p1 < ../do_not_relink_binaries_on_install.diff
}

build() {
  cd $pkgname-$_hg_hash

  rake build revision=$_hg_revision
}

package() {
  cd $pkgname-$_hg_hash

  # regenerate config with proper $pkgdir, revision and cppflags are not needed at this step
  rake config destdir="$pkgdir"
  rake install
}
