# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=tarantool-git
pkgver=1.5.1.r267.g3deb4bf
pkgrel=1
pkgdesc='an in-memory database designed to store the most volatile and highly accessible web content'
arch=(i686 x86_64)
url='http://www.tarantool.org'
license=(BSD)
backup=('etc/tarantool/tarantool.cfg')
# gcc has problems with HEAD tarantool
makedepends=(cmake clang)
# TODO: enable regression tests
#checkdepends(python2-daemon python2-yaml python2-pexpect)
source=('tarantool::git://github.com/tarantool/tarantool#branch=stable'
        'lua-cjson::git://github.com/mpx/lua-cjson.git'
        'luajit::git+http://luajit.org/git/luajit-2.0.git'
        'tarantool.systemd')
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'ad36cc26a66a50e8537570bfed5b620c775a9f85')

pkgver() {
  cd tarantool
  git describe | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd tarantool

  # tarantool uses git submodule, handle this
  git submodule init
  git config submodule.lua-cjson.url $srcdir/third_party/lua-cjson
  git config submodule.luajit.url $srcdir/third_party/luajit
  git submodule update

  CC=clang CXX=clang++ cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_SYSCONF_DIR=/etc/tarantool \
    -DCMAKE_LOCALSTATE_DIR=/var \
    -DENABLE_CLIENT=ON \
    .

  make
}

#check() {
#  cd tarantool
#  make check
#}

package() {
  cd tarantool
  make install DESTDIR="$pkgdir"
  install -D -m644 "$srcdir/tarantool.systemd" "$pkgdir/usr/lib/systemd/system/tarantool.service"
}
