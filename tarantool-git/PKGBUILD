pkgname=tarantool-git
pkgver=1.4.9.93.g93e547a
pkgrel=1
pkgdesc="persistent, transactional in-memory database"
arch=(i686 x86_64)
url="http://www.tarantool.org"
license=(BSD)
depends=()
# gcc has problems with HEAD tarantool
makedepends=(cmake clang)
source=("tarantool::git://github.com/mailru/tarantool#branch=stable"
        'tarantool.systemd')
sha1sums=('SKIP'
          'ad36cc26a66a50e8537570bfed5b620c775a9f85')

pkgver() {
  cd $srcdir/tarantool
  git describe | sed 's/^v//;s/-/./g'
}

build() {
  cd $srcdir/tarantool

  CC=clang CXX=clang++ cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_SYSCONF_DIR=/etc/tarantool \
    -DCMAKE_LOCALSTATE_DIR=/var \
    .

  make
}

package() {
  cd $srcdir/tarantool
  make install DESTDIR=$pkgdir
  install -D -m644 "${srcdir}/tarantool.systemd" "${pkgdir}/usr/lib/systemd/system/tarantool.service"
}
