# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=tarantool-git
pkgver=1.5.2.r38.gd6f2bc7
pkgrel=1
pkgdesc='an in-memory database designed to store the most volatile and highly accessible web content'
arch=(i686 x86_64)
url='http://www.tarantool.org'
license=(BSD)
backup=(etc/tarantool/tarantool.cfg)
depends=(readline ncurses termcap)
# gcc has problems with HEAD tarantool
makedepends=(git cmake clang)
# python2-pexpect dependency is removed in 1.6
checkdepends=(python2-daemon python2-yaml python2-pexpect)
source=(git://github.com/tarantool/tarantool#branch=stable
        git://github.com/mpx/lua-cjson
        git://github.com/tarantool/luajit
        tarantool.systemd)
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'ad36cc26a66a50e8537570bfed5b620c775a9f85')

pkgver() {
  cd tarantool
  git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
  cd tarantool
  find . -type f -exec sed \
      -e 's_^#!/usr/bin/env python$_&2_' \
      -i {} \;
}

build() {
  cd tarantool

  # tarantool uses git submodule, handle this
  git submodule init
  git config submodule.lua-cjson.url "$srcdir/third_party/lua-cjson"
  git config submodule.luajit.url "$srcdir/third_party/luajit"
  git submodule update

  CC=clang CXX=clang++ cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_SYSCONF_DIR=/etc/tarantool \
    -DCMAKE_LOCALSTATE_DIR=/var \
    -DENABLE_CLIENT=ON \
    .

  make
}

check() {
  cd tarantool/test
  ./test-run.py --suite box
}

package() {
  cd tarantool
  install -D -m644 "$srcdir/tarantool.systemd" "$pkgdir/usr/lib/systemd/system/tarantool.service"
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/tarantool/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  make install DESTDIR="$pkgdir"
}
