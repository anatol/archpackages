# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=tarantool-git
pkgver=1.5.3.r65.ged00c73
pkgrel=1
pkgdesc='an in-memory database designed to store the most volatile and highly accessible web content'
arch=(i686 x86_64)
url='http://www.tarantool.org'
license=(BSD)
backup=(etc/tarantool/tarantool.cfg)
depends=(readline ncurses termcap)
# gcc has problems with HEAD tarantool
makedepends=(git cmake clang)
# python2-pexpect dependency is removed in 1.6
checkdepends=(python2-daemon python2-yaml python2-pexpect)
source=(git://github.com/tarantool/tarantool#branch=stable
        git://github.com/mpx/lua-cjson
        git://github.com/tarantool/luajit
        git://github.com/tarantool/sophia.git
        tarantool.systemd)
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'SKIP'
          'ad36cc26a66a50e8537570bfed5b620c775a9f85')

pkgver() {
  cd tarantool
  git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
  cd tarantool
  find . -type f -exec sed \
      -e 's_^#!/usr/bin/env python$_&2_' \
      -i {} \;
}

build() {
  cd tarantool

  # tarantool uses git submodule, handle this
  git submodule init
  git config -f .gitmodules 'submodule.third_party/lua-cjson.url' "$srcdir/third_party/lua-cjson"
  git config -f .gitmodules 'submodule.third_party/luajit.url' "$srcdir/third_party/luajit"
  git config -f .gitmodules 'submodule.third_party/sophia.url' "$srcdir/third_party/sophia"
  git submodule update

  CC=clang CXX=clang++ cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_SYSCONF_DIR=/etc/tarantool \
    -DCMAKE_LOCALSTATE_DIR=/var \
    -DENABLE_CLIENT=ON \
    .

  make
}

check() {
  cd tarantool/test
  ./test-run.py --suite box
}

package() {
  cd tarantool
  make install DESTDIR="$pkgdir"

  install -D -m644 "$srcdir/tarantool.systemd" "$pkgdir/usr/lib/systemd/system/tarantool.service"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm "$pkgdir/usr/share/doc/tarantool/LICENSE"
}
