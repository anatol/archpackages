# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2014.10
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'
arch=(i686 x86_64)
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(glibc)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2)
sha1sums=('243a36acc69fc5645eca4166e5162914c5d995d9')

build() {
  cd u-boot-$pkgver

  # Build tools only
  make defconfig
  make tools-all
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mkimage,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole}
  install -m 644 -D u-boot-$pkgver/doc/mkimage.1 "$pkgdir"/usr/share/man/man1/mkimage.1
}
