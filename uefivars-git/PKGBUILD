# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Murtuza Akhtari <inxsible at gmail dot com>

pkgname=uefivars-git
pkgver=20130101
pkgrel=1
pkgdesc="A tool to populate UEFI Runtime Variables. Requires the kernel module efivars to be loaded."
arch=(i686 x86_64)
url="https://github.com/fpmurphy/Various/tree/master/uefivars-2.0"
license=(GPL)
depends=(efibootmgr)
source=('various::git://github.com/fpmurphy/Various.git')
md5sums=('SKIP')

pkgver() {
  cd $srcdir/various/uefivars-2.0
  echo $(git log -1 --format="%cd" --date=short | sed 's|-||g')
}

prepare() {
  cd $srcdir/various/uefivars-2.0
  # uefivars-git does not respect CFLAGS, fix it
  sed -e 's/CFLAGS = $(EXTRA_CFLAGS)/CFLAGS += $(EXTRA_CFLAGS)/' -i Makefile
}

build() {
  cd $srcdir/various/uefivars-2.0
  make
}

package() {
  cd $srcdir/various/uefivars-2.0
  install -D -m755 src/uefivars/uefivars $pkgdir/usr/bin/uefivars
}
