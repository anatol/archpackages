# Maintainer:
# Contributor:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: tocer <tocer.deng@gmail.com>

pkgname=v8
pkgver=3.22.8
pkgrel=1
pkgdesc='Fast and modern Javascript engine'
arch=('i686' 'x86_64')
url='http://code.google.com/p/v8'
license=('BSD')
depends=('readline' 'icu')
makedepends=('subversion' 'python2')
source=(https://github.com/$pkgname/$pkgname/archive/$pkgver.zip)
sha256sums=('915f3e79244f7d212eba2210ed9f61d432ba211ddc7e8e91b36a581b410d7ef9')

[[ "$CARCH" = 'i686' ]]   && ARCH=ia32
[[ "$CARCH" = 'x86_64' ]] && ARCH=x64

prepare() {
  cd $pkgname-$pkgver

  svn checkout --force http://gyp.googlecode.com/svn/trunk build/gyp --revision 1685
  mkdir -p third_party/icu
  wget http://src.chromium.org/chrome/trunk/deps/third_party/icu46/icu.gyp -O third_party/icu/icu.gyp

  export PYTHON=python2
  find build/ test/ tools/ src/ -type f \
    -exec sed -e 's_^#!/usr/bin/env python$_&2_' \
              -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
              -e 's_^#!/usr/bin/python$_&2_' \
              -e "s_'python'_'python2'_" -i {} \;

  for f in Makefile build/gyp/gyp; do
    sed -i 's/python /python2 /' $f
  done
}

build() {
  cd $pkgname-$pkgver

  build/gyp_v8 -Dv8_enable_i18n_support=1 -Duse_system_icu=1 -Dconsole=readline -Dcomponent=shared_library -Dv8_target_arch=$ARCH -Dwerror= --generator-output=out -f make

  make -C out builddir=`pwd`/out/Release BUILDTYPE=Release mksnapshot.$ARCH
  make -C out builddir=`pwd`/out/Release BUILDTYPE=Release
}

check() {
  cd $pkgname-$pkgver
# Some internationalization tests are failing
#  LD_LIBRARY_PATH=out/Release/lib.target tools/run-tests.py --no-presubmit --outdir=out --buildbot --arch=$ARCH --mode=Release
# --progress=dots
}

package() {
  cd $pkgname-$pkgver

  install -Dm755 out/Release/d8 $pkgdir/usr/bin/d8
  install -Dm755 out/Release/lib.target/libv8.so $pkgdir/usr/lib/libv8.so

  install -d $pkgdir/usr/include
  install -Dm644 include/*.h $pkgdir/usr/include

  install -d $pkgdir/usr/share/licenses/v8
  install -m644 LICENSE* $pkgdir/usr/share/licenses/v8
}
