# Maintainer:
# Contributor:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: tocer <tocer.deng@gmail.com>

pkgname=v8
pkgver=3.19.18.4
pkgrel=2
pkgdesc='Fast and modern Javascript engine'
arch=('i686' 'x86_64')
url='http://code.google.com/p/v8'
license=('BSD')
depends=('gcc-libs' 'readline')
makedepends=('subversion' 'python2')
source=(http://commondatastorage.googleapis.com/chromium-browser-official/$pkgname-$pkgver.tar.bz2)
sha256sums=('10d7a51ed2769b1c2a6311a30ed1967d0a1da3f522c90b9c00f25fbe0b1aba02')

[[ "$CARCH" = 'i686' ]]   && ARCH=ia32
[[ "$CARCH" = 'x86_64' ]] && ARCH=x64

prepare() {
  cd $pkgname-$pkgver

  make dependencies

  export PYTHON=python2
  find build/ test/ tools/ src/ -type f \
    -exec sed -e 's_^#!/usr/bin/env python$_&2_' \
              -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
              -e 's_^#!/usr/bin/python$_&2_' \
              -e "s_'python'_'python2'_" -i {} \;
  sed -i 's/python /python2 /' Makefile
}

build() {
  cd $pkgname-$pkgver

  make $ARCH.release library=shared console=readline werror=no

  cd "$srcdir" && cp -r $pkgname-$pkgver d8-static-$pkgver
  cd d8-static-$pkgver
  make $ARCH.release library=static console=readline werror=no
}

package() {
  cd $pkgname-$pkgver

  install -Dm755 "$srcdir"/d8-static-$pkgver/out/$ARCH.release/d8 "$pkgdir"/usr/bin/d8
  install -Dm755 out/$ARCH.release/lib.target/libv8.so "$pkgdir"/usr/lib/libv8.so

  install -d "$pkgdir"/usr/include
  install -Dm644 include/*.h "$pkgdir"/usr/include

  install -d "$pkgdir"/usr/share/licenses/v8
  install -m644 LICENSE* ${pkgdir}/usr/share/licenses/v8
}

# vim:set ts=2 sw=2 et:
