# Maintainer:
# Contributor:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: tocer <tocer.deng@gmail.com>

pkgname=v8
# use http://omahaproxy.appspot.com/ to find stable v8 version
pkgver=3.20.17.15
pkgrel=2
pkgdesc='Fast and modern Javascript engine'
arch=('i686' 'x86_64')
url='http://code.google.com/p/v8'
license=('BSD')
depends=('readline' 'icu')
makedepends=('subversion' 'python2' 'wget')
# unfortunately https://github.com/$pkgname/$pkgname does not contain all tags
source=(https://commondatastorage.googleapis.com/chromium-browser-official/$pkgname-$pkgver.tar.bz2
        skip_broken_test.diff
        v8.pc)
sha256sums=('3472726f4de152aeb7ea7c382d770f0fcb808c28d74660a58eaa230a7ce32285'
            'db4acf861ebb7078c5ae0d1c0d665cad74773d03b7a88635aec992ac91f31961'
            '2b054309df9af9fb2e3e14527e88360b44745649b4866e592fb357ac90935f5d')

[[ "$CARCH" = 'i686' ]]   && ARCH=ia32
[[ "$CARCH" = 'x86_64' ]] && ARCH=x64

prepare() {
  cd $pkgname-$pkgver

  svn checkout --force http://gyp.googlecode.com/svn/trunk build/gyp --revision 1685
  mkdir -p third_party/icu
  wget http://src.chromium.org/chrome/trunk/deps/third_party/icu46/icu.gyp -O third_party/icu/icu.gyp

  export PYTHON=python2
  find build/ test/ tools/ src/ -type f \
    -exec sed -e 's_^#!/usr/bin/env python$_&2_' \
              -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
              -e 's_^#!/usr/bin/python$_&2_' \
              -e "s_'python'_'python2'_" -i {} \;

  for f in Makefile build/gyp/gyp; do
    sed -i 's/python /python2 /' $f
  done

  # This test fails because of memory overhead (Arch specific compilation flags?)
  patch -p1 < ../skip_broken_test.diff
}

build() {
  cd $pkgname-$pkgver

  build/gyp_v8 -Dv8_enable_i18n_support=1 -Duse_system_icu=1 -Dconsole=readline -Dcomponent=shared_library -Dv8_target_arch=$ARCH -Dwerror= --generator-output=out -f make

  make -C out builddir=`pwd`/out/Release BUILDTYPE=Release mksnapshot.$ARCH
  make -C out builddir=`pwd`/out/Release BUILDTYPE=Release
}

check() {
  cd $pkgname-$pkgver
  LD_LIBRARY_PATH=out/Release/lib.target tools/run-tests.py --no-presubmit --outdir=out --buildbot --arch=$ARCH --mode=Release # --progress=dots
}

package() {
  cd $pkgname-$pkgver

  install -Dm755 out/Release/d8 $pkgdir/usr/bin/d8
  install -Dm755 out/Release/lib.target/libv8.so $pkgdir/usr/lib/libv8.so

  # V8 has several header files and ideally if it had its own folder in /usr/include
  # But doing it here will break all users. Ideally if they use provided pkgconfig file.
  install -d $pkgdir/usr/include
  install -Dm644 include/*.h $pkgdir/usr/include

  sed "s/@VERSION@/$pkgver/g" -i ../v8.pc
  install -d $pkgdir/usr/lib/pkgconfig
  install -m644 ../v8.pc $pkgdir/usr/lib/pkgconfig

  install -d $pkgdir/usr/share/licenses/v8
  install -m644 LICENSE* $pkgdir/usr/share/licenses/v8
}
