# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Thomas Sowell <tom@fancydriving.org>

pkgname=vboot-utils
pkgdesc='Chromium OS verified boot utilities'
pkgver=41.6680
_tag=release-R${pkgver/\./-}.B
pkgrel=1
arch=(i686 x86_64)
url='https://chromium.googlesource.com/chromiumos/platform/vboot_reference'
license=('custom:chromiumos')
depends=(libutil-linux openssl)
makedepends=(libyaml trousers)
source=(vboot-utils-shared-cgpt.patch
        https://chromium.googlesource.com/chromiumos/platform/vboot_reference/+archive/refs/heads/$_tag.tar.gz)
sha1sums=('726cb6ef709dce3f662e3b59a9d6bcb0a637a2d8'
          'SKIP')

prepare() {
  patch -p1 < "$srcdir/vboot-utils-shared-cgpt.patch"

  find -name '*.py' -exec sed -i 's|^#!/usr/bin/python\b|#!/usr/bin/python2|' {} \;
}

build() {
  make
}

check() {
  make runtests
}

package() {
  make DESTDIR="$pkgdir" MINIMAL=1 install
  install -d "$pkgdir"/usr/share/vboot/
  cp -r tests/devkeys "$pkgdir"/usr/share/vboot/devkeys
  install -m 644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
