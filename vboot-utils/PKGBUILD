# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Thomas Sowell <tom@fancydriving.org>

pkgname=vboot-utils
pkgdesc='Chromium OS verified boot utilities'
pkgver=R39.6310
_tag=release-${pkgver/\./-}.B
pkgrel=1
arch=(i686 x86_64)
url='https://chromium.googlesource.com/chromiumos/platform/vboot_reference'
license=('custom:chromiumos')
makedepends=(trousers libyaml util-linux)
source=(vboot-utils-shared-cgpt.patch
        https://chromium.googlesource.com/chromiumos/platform/vboot_reference/+archive/refs/heads/$_tag.tar.gz)

sha1sums=('726cb6ef709dce3f662e3b59a9d6bcb0a637a2d8'
          '9d1a9825a955a8f32ec141e458b142a5fecb774a')

prepare() {
  patch -p1 < "$srcdir/vboot-utils-shared-cgpt.patch"
}

build() {
  make
}

package() {
  make DESTDIR="$pkgdir" MINIMAL=1 install
  install -m 644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
